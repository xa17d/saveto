plugins {
    id "com.android.application"
    id "kotlin-android"
    id "com.google.android.gms.oss-licenses-plugin"
    id "org.jlleitschuh.gradle.ktlint" version "11.5.0"
    id "org.jetbrains.kotlin.plugin.serialization"
}

def keystorePropertiesFile = rootProject.file(".signing/keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}


android {
    namespace "at.xa1.saveto"

    compileSdk versions.compileSdk

    defaultConfig {
        applicationId "at.xa1.safeto"
        minSdk versions.minSdk
        targetSdk versions.targetSdk
        versionCode versions.versionCode
        versionName versions.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        release {
            if (keystorePropertiesFile.exists()) {
                keyAlias keystoreProperties["keyAlias"]
                keyPassword keystoreProperties["keyPassword"]
                storeFile rootProject.file(keystoreProperties["storeFile"])
                storePassword keystoreProperties["storePassword"]
            } else {
                println("ℹ️ Release Keystore files are missing. Check out README.md")
            }
        }
    }


    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion versions.androidx.compose.compiler
        kotlinCompilerVersion versions.kotlin
    }
    packagingOptions {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
    }
}

dependencies {
    implementation lib.kotlin.stdlib
    implementation lib.android.material
    implementation lib.androidx.appcompat
    implementation lib.androidx.compose.ui
    implementation lib.androidx.compose.material
    implementation lib.androidx.compose.uiPreview
    implementation lib.androidx.activity.compose
    implementation lib.kotlinx.coroutines.android
    implementation lib.kotlinx.serialization.json
    implementation lib.google.accompanist.pager
    implementation lib.google.accompanist.pagerIndicators
    implementation lib.google.play.ossLicenses

    testImplementation lib.junit
    testImplementation lib.kotlin.test
    testImplementation lib.kotlinx.coroutines.test

    androidTestImplementation lib.androidx.test.ext
    androidTestImplementation lib.androidx.test.espresso
    androidTestImplementation lib.androidx.compose.uiTest
    androidTestImplementation lib.kotlin.test

    debugImplementation lib.androidx.compose.uiTooling
    debugImplementation lib.androidx.compose.uiTestManifest
}
